apply plugin: 'application'
apply plugin: 'java'

repositories {
    jcenter()
}

dependencies {
  // Logging
  compile 'ch.qos.logback:logback-classic:1.1.7'
  compile 'ch.qos.logback:logback-core:1.1.7'
  compile 'org.slf4j:slf4j-api:1.7.20'

  testCompile 'junit:junit:4.12'
  testCompile 'org.apache.commons:commons-io:1.3.2'
  testCompile 'org.jsoup:jsoup:1.8.3' // Reading HTML output
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.praqma.metricviz.Main'

jar {
  version = '0.1.0'
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

test {
  afterTest { desc, result ->
    println "${desc.name} [${desc.className}] : ${result.resultType}"
  }

  testLogging {
    exceptionFormat 'full'
  }
}

run {
  def arguments = []
  if (project.hasProperty('input')) {
    arguments.push("--input=${input}")
  }
  if (project.hasProperty('output')) {
    arguments.push("--output=${output}")
  }
  args arguments
}
